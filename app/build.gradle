plugins {
    id "com.android.application"
}
apply from: '../dependencies.gradle'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.auth0.android.lock.app"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "demo"]
    }

    signingConfigs {
        release {
            storeFile file("release-test.jks")
            storePassword "android"
            keyAlias "android"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    testImplementation testDeps.junit
    implementation androidDeps.appCompat
    implementation androidDeps.material
    implementation project(':lock')
    debugApi project(":auth0-debug-local-snapshot-2.29.0")
    releaseApi project(":auth0-release-local-snapshot-2.29.0")
    implementation auth.values()
}
